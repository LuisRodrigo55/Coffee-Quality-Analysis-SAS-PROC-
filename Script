 SAS Project: Coffee Quality Analysis using PROC Functions               
-------------------------------------------------------------------------
This script contains 8 categories of SAS PROCs for data science (most common and used).

IMPORT DATA
-------------------------------------------------------------------------
This is the import of the data into the SAS Environment, the table is allocated in the work space

FILENAME COFFEE_FILE 'uploaded:Coffee-modified.csv';

PROC IMPORT DATAFILE=COFFEE_FILE
    OUT=work.Coffee
    DBMS=CSV
    REPLACE;
    GETNAMES=YES;
RUN;

TITLE 'Coffee Quality Data Analysis';

/* Data Visualization with the PROC PRINT, this will display the content of the table*/
TITLE3 'Data Visualization (PROC PRINT)';
PROC PRINT DATA=work.Coffee (OBS=20); /*We will display the first 20 observations */
    VAR ID Country_of_Origin Variety Total_Cup_Points Aroma Flavor;
    LABEL Total_Cup_Points='Total Score';
RUN;

/* Data Sorting (PROC SORT) This will arrange the observations by the 4 most relevant variables (Total Score, Aroma, Country, Variety), it also will prepares the data for ordered reporting or group processing. */

TITLE3 'Data Sorting (PROC SORT)';

PROC SORT DATA=work.Coffee OUT=work.Coffee_Sorted;
    BY DESCENDING Total_Cup_Points DESCENDING Aroma Country_of_Origin Variety;
RUN;

/* Preview to validate its printing the top 5 records */
PROC PRINT DATA=work.Coffee_Sorted (OBS=5);
    VAR Total_Cup_Points Aroma Country_of_Origin Variety;
RUN;

/*Data summary and calculation of mean, median, standard deviation, min, and max for key scores */

TITLE3 'Summary(PROC MEANS)';

PROC MEANS DATA=work.Coffee N MEAN MEDIAN STD MIN MAX;
    VAR Total_Cup_Points Aroma Flavor Acidity Body Balance Sweetness;
    CLASS Country_of_Origin; /* Group statistics by country*/
RUN;

TITLE3 'Detailed Univariate Statistics (PROC UNIVARIATE)';

PROC UNIVARIATE DATA=work.Coffee PLOT;
    VAR Total_Cup_Points;
    OUTPUT OUT=work.Total_Score_Stats MEAN=Mean_Score STD=StdDev_Score; /* Save key stats to a new dataset */
RUN;



/*Frequency and Crosstabulation Tables (PROC FREQ), this section shows the amount of observations and relationships between categorical variables*/

TITLE3 'Frequency and Crosstabulation Tables (PROC FREQ)';

PROC FREQ DATA=work.Coffee;
    TABLE Country_of_Origin / OUT=work.Country_Counts NOPRINT; /* Save counts without printing*/
RUN;

/* Two-way Crosstabulation, Processing Method vs. Species */
PROC FREQ DATA=work.Coffee;
    TABLE Processing_Method * Species / CHISQ EXPECTED DEVIATION; /* Add chi-square test for association */
    TITLE4 'Processing Method by Coffee Species';
RUN;





/*Reporting (PROC REPORT), this section creates customized, professional, and complex reports */

TITLE3 'Custom Reporting (PROC REPORT)';

/* Create a detailed report summarizing average quality scores by Country and Variety */
PROC REPORT DATA=work.Coffee_Sorted NOWINDOW HEADSKIP;
    COL Country_of_Origin Variety N Total_Cup_Points Aroma Flavor Acidity;

    /* Define Grouping Variables */
    GROUP Country_of_Origin;
    DEFINE Country_of_Origin / GROUP 'Country';
    DEFINE Variety / DISPLAY 'Variety';

    /* Define Summary Statistics for Numerical Variables */
    DEFINE N / N 'Count' FORMAT=5.;
    DEFINE Total_Cup_Points / MEAN FORMAT=6.2 'Average Total Score';
    DEFINE Aroma / MEAN FORMAT=5.2 'Avg Aroma';
    DEFINE Flavor / MEAN FORMAT=5.2 'Avg Flavor';
    DEFINE Acidity / MEAN FORMAT=5.2 'Avg Acidity';

    BREAK AFTER Country_of_Origin / SKIP; /* Add a line break after each country group */

    RBREAK AFTER / SUM; /* Add a grand total row */
RUN;








/* Statistical Modeling (PROC REG), The idea is to run a simple linear regression to see if Aroma and Flavor predict the Total Score */

TITLE3 'Statistical Modeling (PROC REG)';


PROC REG DATA=work.Coffee;
    MODEL Total_Cup_Points = Aroma Flavor Aftertaste Acidity Body Balance;
    PLOT RESIDUAL. * PREDICTED. / HLABEL='Predicted Score' VLABEL='Residuals';
    OUTPUT OUT=work.Reg_Results P=Predicted_Score R=Residual_Value; 
RUN;
QUIT; 


/* Data Management/File I/O (PROC EXPORT and PROC COPY), here the idea is to export the regression results*/

TITLE3 'Data Export (PROC EXPORT)';

PROC EXPORT DATA=work.Reg_Results
    OUTFILE='./Reg_Results_Export.csv' 
    DBMS=CSV
    REPLACE;
RUN;



TITLE3 'File Management (PROC COPY)';

/* I leave this code as reference, the PROC COPY, copies entire SAS files datasets from one library to another. Here, we copy from the temporary WORK library to the permanent USER library. */

PROC COPY IN=WORK OUT=USER;
    SELECT Coffee_Sorted; /* Copy only the sorted dataset */
RUN;


/* This step is used to confirm the copy by printing a small part of the dataset from the library*/

PROC PRINT DATA=USER.Coffee_Sorted (OBS=2);
RUN;


/* SQL Operations (PROC SQL), here I have some examples of SQL code within SAS */

TITLE3 'SQL Operations (PROC SQL)';



PROC SQL;
    TITLE4 'Filter the Best Coffees';

    SELECT
        Country_of_Origin,
        Owner,
        Total_Cup_Points,
        Aroma,
        Acidity,
        Processing_Method
    FROM
        work.Coffee
    WHERE
        Total_Cup_Points >= 88
    ORDER BY
        Total_Cup_Points DESC;



    TITLE4 'Example of Aggregate Statistics (GROUP BY)';

    SELECT
        Variety,
        COUNT(ID) AS Num_Samples,
        AVG(Total_Cup_Points) AS Average_Score FORMAT=5.2
    FROM
        work.Coffee
    WHERE
        Variety NOT IN ('Other', 'Unknown', '') 
    GROUP BY
        Variety
    HAVING
        COUNT(ID) > 50 
    ORDER BY
        Average_Score DESC;

QUIT;







